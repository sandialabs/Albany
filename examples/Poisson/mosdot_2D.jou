#     {wsub = 20}
#     {tsub = 10}
#     {tox = 3}
#     {wgate = 5}
#     {tgate = 1}
#     {tgateox = 1}
#     {wquantum = 8}
#     {tquantum = 5 }
#     {toxquantum = 1}

# vertices for Silicon substrate
create vertex {-wsub/2.0} {-tsub} 0
create vertex {wsub/2.0} {-tsub} 0
create vertex {wsub/2.0} 0 0
create vertex {-wsub/2.0} 0 0

# vertices for SiO2 region
create vertex {-wsub/2.0} {tox} 0
create vertex {wsub/2.0} {tox} 0

# vertices for gate region
create vertex {-wsub/2.0} {tgateox} 0
create vertex {-wsub/2.0 + wgate} {tgateox} 0
create vertex {-wsub/2.0 + wgate} {tgate+tgateox} 0
create vertex {-wsub/2.0} {tgate+tgateox} 0

create vertex {wsub/2.0-wgate} {tgateox} 0
create vertex {wsub/2.0} {tgateox} 0
create vertex {wsub/2.0} {tgate+tgateox} 0
create vertex {wsub/2.0-wgate} {tgate+tgateox} 0

# create the Silicon region (surface 1)
create surface vertex 1 2 3 4

# create the SiO2 region (surface 2)
create surface vertex 3 4 5 6

# create the left gate region (surface 3)
create surface vertex 7 8 9 10

# create the right gate region (surface 4)
create surface vertex 11 12 13 14

# merge all overlapped curves
merge all

#remove portion of Si02 where left gate is 
#(destroys surface 3, creates 5)
subtract 3 from 2

#remove portion of Si02 where right gate is 
#(destroys surface 3, creates 6)
subtract 4 from 2


# re-create the gate regions
create surface vertex 17 18 19 20
create surface vertex 21 22 23 24

# merge all overlapped curves
merge all

#create vertices for the quantum regions
create vertex {-wquantum/2.0} {-tquantum} 0
create vertex {wquantum/2.0} {-tquantum} 0
create vertex {wquantum/2.0} {toxquantum} 0
create vertex {-wquantum/2.0} {toxquantum} 0

# create the quantum silicon region (surface 9)
create surface vertex 33 34 35 36

# imprint quantum region on other surfaces
imprint all

#Remove extra volume
delete volume 7

merge all

## define nodesets for BCs
nodeset 1 curve in surface 7
nodeset 1 name "lgate"
color nodeset 1 red

nodeset 2 curve in surface 8
nodeset 2 name "rgate"
color nodeset 2 red

nodeset 3 curve 1
nodeset 3 name "substrate"
color nodeset 3 blue

nodeset 4 curve 7
nodeset 4 name "topgate"
color nodeset 4 green

nodeset 5 curve 2 4 18 19 25 26
nodeset 5 name "sides"
color nodeset 5 green

nodeset 6 curve 42 43 44
nodeset 6 name "interface"
color nodeset 5 blue


# define blocks (each block represents one material)
block 1 surface 10
block 1 name "silicon"
block 1 element type quad4

block 2 surface 13
block 2 name "sio2"
block 2 element type quad4

block 3 surface 7 8
block 3 name "polysilicon"
block 3 element type quad4

block 4 surface 11
block 4 name "silicon.quantum"
block 4 element type quad4

block 5 surface 12
block 5 name "sio2.quantum"
block 5 element type quad4


# set mesh size
surface 13 7 8 size 1
surface 10 size 1


#mesh surfaces
mesh surface all

# refine mesh in Silicon near the Silicon/SiO2 interface
# one refinement generates 1800 elements
refine curve 42 43 44 numsplit 1 bias 1.2 depth 3 smooth

##refine curve 42 43 44 numsplit 1 bias 1.2 depth 5 smooth
#refine surface 11 13 numsplit 2 bias 1 depth 0 smooth
##refine surface 11 numsplit 2 bias 1 depth 0 smooth
##refine surface 11 numsplit 1 bias 1 depth 3 smooth
#refine curve 44 numsplit 2 bias 1.2 depth 2 smooth

## merge all overlapped elements
merge all

# dimension size is assumed in [um]
# output the Exodus II mesh
export mesh "mosdot_2D.exo" dimension 2 block all overwrite
